buildscript {
    ext.kotlin_version = kff_kotlin_version
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
        classpath 'org.parchmentmc:librarian:1.+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'kotlin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

version = "${minecraft_version}-${mod_version}"
group = "${mod_author}.${modId}"
archivesBaseName = modId

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)

minecraft {
    mappings channel: 'parchment', version: mappings_version

    runs {
        configureEach {
            workingDirectory project.file('run')
            arg "-mixin.config=${mixin_config}"
            mods { modId { source sourceSets.main } }
        }

        client {}

        server { args '--nogui' }

        data {
            args '--mod', modId, '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.java { srcDir 'src/shared/java' }
sourceSets.main.kotlin { srcDir 'src/shared/kotlin' }
sourceSets.main.resources { srcDir 'src/shared/resources' }
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { name = 'kotlinforforge'; url = 'https://thedarkcolour.github.io/KotlinForForge/' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
    implementation "thedarkcolour:kotlinforforge:${kff_version}"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_displayName,
                "Specification-Vendor"    : mod_author,
                "Specification-Version"   : "${minecraft_version}-${mod_version}",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${minecraft_version}-${mod_version}",
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "${mixin_config}"
        ])
    }
    finalizedBy('reobfJar')
}

mixin {
    add sourceSets.main, "betterdurability.refmap.json"
}
